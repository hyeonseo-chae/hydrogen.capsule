action (FindStore) {
  description (위치에 기반하여 가게를 찾습니다)
  type(Search)
  
  collect {
     input (self) {
      type (self.Self)
      min (Optional) max (One)
    }
    
    input (near) {
      type (Near)
      min (Optional) max (One)
    }
    
    input (where1) {
      type (Where1)
      min (Optional) max (One)
    }
    
    input (addvoca) {
      type (Addvoca)
      min (Optional) max (One)
    }
    
    computed-input (point){
      type (Point)
      min (Optional) max (One)
      compute{
        if(exists(near)){
         
          intent{
            goal: Point
            route: geo.CurrentLocation
          }         
        }
      }
    }
    
    // 여러가지 input을 한 가지의 input처럼 사용할 때 사용 (https://bixbydevelopers.com/dev/docs/reference/type/action.collect.input-group)
    input-group(search){
      requires (OneOf)
      collect{
        
        // Geo 라이브러리 캡슐 사용 (https://bixbydevelopers.com/dev/docs/dev-guide/developers/library.geo)
        input (location1){
          type(geo.SearchRegion)
          min (Optional) max (One)
          
          // 기본적으로 selection learning 사용하시려면 selection strategy을 직접 구현
          // 하지만, 라이브러리 캡슐의 concept을 사용하는 경우, 이미 해당 캡슐 내에 구현이 되어 있어 따로 selection strategy을 만드실 필요는 없음
          // 참조 (https://bixbydevelopers.com/dev/docs/dev-guide/developers/learning.selection-learning#enabling-selection-learning)
          default-select {
            with-learning
          }   
        }
        
        input (place) {
          type (geo.PlaceName)
          min (Optional) max (One)
        }
      }
    }
  }
  
  output (StoreInfo){
    // 결과에 대한 예외처리 진행, 자바스크립트에서 던진 에러를 처리 가능
    throws{
      error (NoResult){
        on-catch {
          halt {
            dialog {
              template-macro (no_result)
            }
          }
        }
      }
      unknown-error{
        on-catch {
          halt {
            dialog {
              template-macro (no_result)
            }
          }
        }
      }
    }
  }
}
